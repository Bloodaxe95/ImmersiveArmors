plugins {
    id 'java-library'
    id 'architectury-plugin' version '3.2-SNAPSHOT'
    id 'dev.architectury.loom' version '0.7.2-SNAPSHOT' apply false
}

def getAppVersion = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    def commitId = stdout.toString().replace("\n", "").replace("\r", "").trim()
    stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'tag', '--points-at', commitId
        standardOutput = stdout
    }
    def tagName = stdout.toString().replace("\n", "").replace("\r", "").trim()
    def versionName = 'git-' + commitId
    if (tagName != null && "" != tagName) {
        versionName = tagName
    }
    return versionName
} as Object

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: 'dev.architectury.loom'

    loom {
        mixinConfig "immersive_armors.mixin.json"
    }

    dependencies {
        minecraft "com.mojang:minecraft:${project.minecraft_version}"
        mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    }
}

allprojects {
    apply plugin: "architectury-plugin"

    targetCompatibility = JavaVersion.VERSION_1_8
    sourceCompatibility = JavaVersion.VERSION_1_8

    project.version = getAppVersion()

    group = rootProject.group
    description = rootProject.displayname
    archivesBaseName = rootProject.name

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }
}
